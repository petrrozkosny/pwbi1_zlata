01 suma srazek mezirocne = 


-- suma srazek v danem roce
-- suma srazek z tabulky data filtrovane vizualem pres sloupec rok
SUM(data[srazky])
-- mínus
-
-- suma srazek v predchozim roce
--krok3: Scitame sloupec srazky tabulky vracene funkci FILTER()
SUMX(
    --krok2: Funkce FILTER() nastavuje filtr na sloupec tabulky
    -- vracene funkci ALL()
    FILTER(
        --krok1: Funkce ALL() vytvari kopii tabulky data zbavenou
        -- vsech filtru
        ALL(data),
        -- Z originalni tabulky data stale filtrovane vizualem
        -- dopocitavame maximalni hodnotu ze sloupce rok, coz je
        -- cislo daneho roku, od ktereho odecitame 1
    data[rok] = MAX(data[rok])-1),
data[srazky])
____________________________________________________________________-

02 pocet vyskytu teplota nad 30 = 

-- pocitame hodnoty ve sloupci datum tabulky vracene funkci FILTER()
COUNTX(
    -- funkce FILTER() vraci tabulku filtrovanou pres sloupce rok a teplota
    FILTER(
        -- tabulka, jejiz kopii delame, respektujeme filtry nad rokem
        data,
    -- nove nastaveny filtr
    data[teplota] >30),
data[datum])

_______________________________________________________________________________

03 maximalni teplota rok 2023 = 
MAXX(
    FILTER(data, data[rok] = 2023),
data[teplota])

__________________________________________________________________________

05 podil dnu se srazkami = 

-- pocet dnu srazky > 0
COUNTX(
    FILTER(data,
    data[srazky] > 0),
data[datum])
/
-- pocet dnu kdy srazky nejsou prazdne
COUNT(data[srazky])

_________________________________________________________________________

06 maximalni teplota posledni rok = 

-- maximum ze sloupce teplota pro danou lokalitu
-- za maximalni rok napric lokalitami

MAXX(
    FILTER(data,
    data[rok] = 
    -- maximalni hodnota ze sloupce rok tabulky
    -- zbavene vsech filtru
    MAXX(ALL(data),data[rok])),
data[teplota])

___________________________________________________________

08 maximaln teplota predchozi rok = 

MAXX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok]) - 1
    && data[lokalita] IN VALUES(data[lokalita])),
data[teplota])

______________________________________________________

09 pf maximalni teplota = 
IF(
-- maximalni teplota v danem roce a dane lokalite
MAX(data[teplota])
= 
-- maximalni teplota ve vsech letech a dane lokalite
MAXX(
    FILTER(
        ALL(data),
    data[lokalita] IN VALUES(data[lokalita])),
data[teplota]),"TOMATO")

___________________________________________________________

10 nazev grafu = 
"Vývoj maximální teploty v lokalitách: "&
CONCATENATEX(
    DISTINCT(data[lokalita]),data[lokalita],", ")

___________________________________________________________

11 suma srazek vybrany rok = 
IF(
    -- ptame se na uzivatelem vybranou hodnotu ve sloupci typ_obdobi
    -- testujeme, jestli se vybrana hodnota rovna "prubezne"
    SELECTEDVALUE(obdobi[typ_obdobi]) = "prubezne",
-- POKUD SE VYBRANÁ HODNOTA ROVNA "PRUBEZNE"
-- v lokalite a roce vybranem uzivatelem, mesici vybranem vizualem
SUM(data[srazky]),
-- POKUD SE VYBRANA HODNOTA NEROVNA "PRUBEZNE"
-- v lokalite a roce vybranem uzivatelem, za dany a predchozi mesice
-- tj. ytd
SUMX(
-- Funkce FILTER() nastavuje filtry na sloupce tabulky vracene funkci ALL()
FILTER(
    -- Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
    ALL(data),
    -- PODMINKY
data[lokalita] = SELECTEDVALUE(data[lokalita])
&& data[rok] = SELECTEDVALUE(data[rok])
&& data[mesic] <= SELECTEDVALUE(data[mesic])),
-- KONEC POMINEK
-- sloupec tabulky vracene funkci FILTER(), ktery funkce SUMX() agreguje
data[srazky])
)



